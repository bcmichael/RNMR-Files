; macro for setting cp ramps

macarg name,chan,ctr,width

dflt name,cpdly,'enter name of ramp'
dflt chan,1,'enter chan number of ramp'

pwx &chan &name&chan'v1' >>oldbegin
pwx &chan &name&chan'v2' >>oldend
calc 4 ndec &oldbegin &oldend add 2 div >>oldctr
calc 4 ndec &oldend &oldbegin sub >>oldwidth
dflt ctr,&oldctr,'enter the center of the sweep in pwx units'
dflt width,&oldwidth,'enter the width of the ramp in pwx units (neg for down)'

; sets ramp to (ctr-width/2) to (ctr+width/2)
; or truncates as needed... i.e. -5 to +5 becomes 0 to +5

calc 4 ndec &width 2 div >>temp
calc 4 ndec &ctr &temp sub >>begin
calc 4 ndec &begin &width add >>end

; checking limits to be valid:
calc &begin 0 LT >>tst
ifeq /log &tst 1 . .+2
  msg "&macro$ - WARNING: truncating ramp at pwx 0"
  lclarg begin 0

calc &end 100 GT >>tst
ifeq /log &tst 1 . .+2
  msg "&macro$ - WARNING: truncating ramp at pwx 100"
  lclarg end 100

; get current pwxex 1
pwxex &chan >>expname


pwx &chan &name&chan'v1' &begin
pwx &chan &name&chan'v2' &end

; skip in case no previous pwx tables were loaded:
tst /false eq "&expname" ""
  pwxex &chan &expname
endtst

msg "&name on ch. &chan - center: &ctr, width: &width (&begin - &end\)"
