macarg start end step dstrec param opt1 opt2

; check for presence of required macros:
macro reqchk
reqchk mtools
  rtnarg &error
tst eq &error 1
  mexit
endtst

setidn >>idn0,>>idn1
setidn "&macro$",*
unit /time >>tunits
unit /time usec
onerr .exit

tst /false lcl param
  msg "Paropt 1.start 2.end 3.step 4.record 5.parameter 6.channel 7.number"

  dflt  START,1,"Enter starting value"
  dflt  END,2,"Enter end value"
  dflt  STEP,1,"Enter step size"
  ptra >>rdrec >>wrrec
  mtools targetrec &wrrec &dstrec
    rtnarg dstrec
  dflt  PARAM,RD,"Enter parameter to change"
  DFLT  opt1,-1,"Enter channel/index [-1 if unneeded]"
  tst eq &opt1 -1
    lcldl opt1
  elstst
    DFLT  opt2 -1 "Enter index [-1 if unneeded]"
    tst eq &opt2 -1
      lcldl opt2
    endtst
  endtst
endtst

CALC 4 ndec &END &START SUB >>RANGE
CALC -1 ndec &RANGE &STEP DIV 1 add >>STEPS
CALC 4 NDEC &STEPS 1 SUB &STEP MUL &START ADD >>ENDPNT
tst eq &param LS
  calc -1 ndec &endpnt >>endpnt
endtst
msg "The final value will be &ENDPNT"

; allocating destination record and setting parameters for second dimension
size >>sizeaa

mtools targetrec 5 &dstrec
allb &DSTREC 2 &sizeaa &STEPS 1

set rec &DSTREC,2,DIM,2
set rec &DSTREC,2,DOM,UNKN
set rec &DSTREC,2,SYN,1
set rec &DSTREC,2,FIRST,&START
set rec &DSTREC,2,STEP,&STEP
set rec &DSTREC,2,ACQTYP,REAL


titlea "Paropt, &PARAM &opt1 &opt2, &START - &ENDPNT - by &STEP &note"

tst lcl mac
  &param &opt1 &opt2 /paropt
    rtnarg initial
elstst
  &PARAM &opt1 &opt2 >>INITIAL
endtst

&PARAM &opt1 &opt2 &START

DO /LCL 1, &STEPS, LNR
  CALC 4 NDEC &lnr 1 SUB &STEP mul &START ADD >>CUR
  ; Make sure LOOP/LS values are integers:
  tst eq &param LS
    calc -1 ndec &cur >>cur
  endtst
  &param &opt1 &opt2 &cur
  tst lcl ppupdate
    ppupdate
  endtst
  setidn "&macro$","&lnr / &steps"
  set info lcl pretime
    date /epoch
  ; optional delay before each acq
  tst lcl wttim
    msg "waiting &wttim sec..."
    wttim &wttim
  endtst
  DOB2B &DSTREC
  set info lcl posttime
    date /epoch

  CALCi &posttime &pretime SUB >>secs

  msg "measurement took &secs secs"

  CALC 1 NDEC &STEPS &lnr SUB &secs MUL 60 DIV >>minutes
  CALC -1 NDEC &minutes 60 DIV >>hrs
  CALC 2 NDEC &minutes 60 mod >>minutes


  msg "&PARAM &opt1 &opt2 = &CUR done. Rec: &DSTREC -
  Remaining: &hrs hrs &minutes min."
ENDDO

; reset value to original:
&PARAM &opt1 &opt2 &INITIAL

set info lcl date time
  date
savarv 1

.exit
setidn &idn0,&idn1
unit /time &tunits
mexit
